# qry_get_geography_data.sparql
# Fetch statistical data for:
# - selected geography framework
# - time period
# - selected indicator
# Used R function get_geography_data function.


# Prefixes
# As derived from https://statistics.gov.scot/sparql
PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX qb: <http://purl.org/linked-data/cube#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX sdmx: <http://purl.org/linked-data/sdmx/2009/concept#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX void: <http://rdfs.org/ns/void#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
# Added for convenience 
PREFIX dim: <http://purl.org/linked-data/sdmx/2009/dimension#>

# Query
SELECT DISTINCT ?geo ?time ?measure ?value ?age ?gender
WHERE {

	?x dim:refArea ?refArea .	
	?x dim:refArea/rdfs:label ?geo .

	?x qb:dataSet <http://statistics.gov.scot/data/job-seekers-allowance> .
	
	?x dim:refPeriod/rdfs:label ?time .
	
	?x qb:measureType/rdfs:label ?measure .

	?x <http://statistics.gov.scot/def/dimension/age>/rdfs:label ?age .

	?x <http://statistics.gov.scot/def/dimension/gender>/rdfs:label ?gender .

	?x <http://statistics.gov.scot/def/measure-properties/count> |
			 <http://statistics.gov.scot/def/measure-properties/ratio>	?value .
	
	FILTER (?time IN ("2001-Q1", "2001-Q2"))
	FILTER (?geo IN ("S01001912"))
}
